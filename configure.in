dnl Process this file with autoconf to produce a configure script.
AC_INIT(src/init.c)

AM_INIT_AUTOMAKE([oleo], [1.6.6])
GNU_PACKAGE="GNU $PACKAGE"
AC_DEFINE_UNQUOTED(GNU_PACKAGE, "$GNU_PACKAGE")

dnl Define available languages
ALL_LINGUAS=""

AC_ARG_PROGRAM
AM_CONFIG_HEADER(config.h)

dnl Detect Cygwin32 environment
AM_CYGWIN32

dnl Checks for programs.
AC_PROG_YACC
AC_PROG_CC
AM_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_RANLIB

dnl Checks the Compiler.
AM_C_PROTOTYPES

dnl Checks for libraries.
dnl Replace `main' with a function in -lncurses:
AC_CHECK_LIB(ncurses, main, ,
	AC_CHECK_LIB(curses, main, ,
		AC_CHECK_LIB(termlib, tgetent, ,
			AC_CHECK_LIB(termcap, tgetend))))

dnl Replace `main' with a function in -lm:
AC_CHECK_LIB(m, main)

dnl Checks for X11
AC_PATH_XTRA

CFLAGS="$CFLAGS $X_CFLAGS"
LIBS="$LIBS $X_LIBS"

dnl Checks for X11 Libraries
AC_CHECK_LIB(X11, main)

dnl Checks for Guile
AC_CHECK_LIB(guile, scm_boot_guile)

dnl Checks if -DDEBUG is requested
AC_ARG_WITH(debug,
[  --with-debug                   Send debugging info to stderr],
[              case "${withval}" in
                 y | ye | yes )         usedebug=yes ;;
                 n | no )               usedebug=no ;;
                 * )                    usedebug=yes ;;
               esac],
[              with_debug=no])

if test $with_debug = yes; then
  CFLAGS="$CFLAGS -DDEBUG"
  if test "x$GCC" = "xyes"; then
    if test -z "`echo "$CFLAGS" | grep "\-Wall" 2> /dev/null`"; then
      CFLAGS="$CFLAGS -Wall"
    fi
  fi
fi
           
dnl Checks for GTK
AC_ARG_WITH(gtk,
[  --without-gtk		  Do not use GTK, even if detected],
[		case "${withval}" in
		  y | ye | yes )	usegtk=yes ;;
		  n | no )		usegtk=no ;;
		  * )			usegtk=yes ;;
		esac],
[		with_gtk=yes])

if test $with_gtk = yes; then
	AM_PATH_GTK
fi

dnl Checks for header files.
AC_HEADER_DIRENT
AC_HEADER_STDC
AC_CHECK_HEADERS(fcntl.h limits.h malloc.h strings.h sys/time.h unistd.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_INLINE
AC_TYPE_SIZE_T
AC_HEADER_TIME
AC_STRUCT_TM
AC_STRUCT_TIMEZONE

dnl Checks for library functions.
AC_FUNC_ALLOCA
AC_TYPE_SIGNAL
AC_FUNC_STRFTIME
AC_FUNC_VPRINTF
AC_CHECK_FUNCS(ftime gethostname gettimeofday mktime select strdup strstr)
AM_FUNC_MKTIME
AM_FUNC_OBSTACK

dnl See if we're GNU Gettext friendly
AM_GNU_GETTEXT

AC_SUBST(LIBOBJS)

AC_OUTPUT([Makefile oleobug doc/Makefile lib/Makefile src/Makefile intl/Makefile po/Makefile.in], [sed -e "/POTFILES =/r po/POTFILES" po/Makefile.in > po/Makefile])
