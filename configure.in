dnl
dnl This is the configuration file for GNU oleo.
dnl $Id: configure.in,v 1.22 1998/08/26 13:45:59 danny Exp $
dnl
dnl Process this file with autoconf to produce a configure script.
dnl
AC_INIT(src/init.c)

AM_INIT_AUTOMAKE([oleo], [1.6.6])
GNU_PACKAGE="GNU $PACKAGE"
AC_DEFINE_UNQUOTED(GNU_PACKAGE, "$GNU_PACKAGE")

dnl Define available languages
ALL_LINGUAS=""

AC_ARG_PROGRAM
AM_CONFIG_HEADER(config.h)

dnl Detect Cygwin32 environment
AM_CYGWIN32

dnl Checks for programs.
AC_PROG_YACC
AC_PROG_CC
AM_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_RANLIB

dnl Checks the Compiler.
AM_C_PROTOTYPES

dnl Checks for libraries.
AC_CHECK_LIB(ncurses, waddch, ,
	AC_CHECK_LIB(curses, addch, ,
		AC_CHECK_LIB(termlib, tgetent, ,
			AC_CHECK_LIB(termcap, tgetend))))

dnl This library is required on FreeBSD.
AC_CHECK_LIB(mytinfo, tputs)

dnl Check existence of a working math library
AC_CHECK_LIB(m, sqrtf)

dnl Checks for X11
AC_PATH_XTRA

dnl
dnl Motif (LessTif)
dnl
dnl If user selects Motif, deselect GTK even if the user requested it.
dnl
AC_ARG_WITH(motif,
[  --without-motif		  Do not use Motif, even if detected],
[		case "${withval}" in
		  y | ye | yes )	usemotif=yes ;;
		  n | no )		usemotif=no ;;
		  * )			usemotif=yes ;;
		esac],
[		with_motif=yes])

dnl
dnl Checks for Motif
dnl These should not happen if --without-motif has been used.
dnl
if test $with_motif = yes; then
  AC_FIND_MOTIF
  ICE_FIND_Xbae
  ICE_FIND_XmHTML
  ICE_FIND_SciPlot
fi

dnl
dnl This is an automake variable which allows you to use conditionals
dnl in makefiles. We'll use it in the src/ directory to figure out
dnl which files to compile.
dnl
AM_CONDITIONAL(UseMotif, test x$with_motif = xyes)

dnl Checks for Guile
dnl AC_CHECK_LIB(guile, scm_boot_guile)

dnl Checks if -DDEBUG is requested
AC_ARG_WITH(debug,
[  --with-debug                   Send debugging info to stderr],
[              case "${withval}" in
                 y | ye | yes )         usedebug=yes ;;
                 n | no )               usedebug=no ;;
                 * )                    usedebug=yes ;;
               esac],
[              with_debug=no])

if test $with_debug = yes; then
  CFLAGS="$CFLAGS -DDEBUG"
  if test "x$GCC" = "xyes"; then
    if test -z "`echo "$CFLAGS" | grep "\-Wall" 2> /dev/null`"; then
      CFLAGS="$CFLAGS -Wall"
    fi
  fi
fi
           
if test $with_motif = no; then
dnl Checks for GTK
AC_ARG_WITH(gtk,
[  --without-gtk		  Do not use GTK, even if detected],
[		case "${withval}" in
		  y | ye | yes )	usegtk=yes ;;
		  n | no )		usegtk=no ;;
		  * )			usegtk=yes ;;
		esac],
[		with_gtk=yes])

if test $with_gtk = yes; then
AM_PATH_GTK(1.0.0, AC_DEFINE(HAVE_LIBGTK))
fi
fi

dnl Checks for header files.
AC_HEADER_DIRENT
AC_HEADER_STDC
AC_CHECK_HEADERS(fcntl.h limits.h malloc.h strings.h sys/time.h unistd.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_INLINE
AC_TYPE_SIZE_T
AC_HEADER_TIME
AC_STRUCT_TM
AC_STRUCT_TIMEZONE

dnl Checks for library functions.
AC_FUNC_ALLOCA
AC_TYPE_SIGNAL
AC_FUNC_STRFTIME
AC_FUNC_VPRINTF
AC_CHECK_FUNCS(ftime gethostname gettimeofday mktime select strdup strstr)
AM_FUNC_MKTIME
AM_FUNC_OBSTACK

dnl See if we're GNU Gettext friendly
AM_GNU_GETTEXT

AC_LINK_FILES($nls_cv_header_libgt, $nls_cv_header_intl)

AC_SUBST(LIBOBJS)

AC_SUBST(include_motif)
AC_SUBST(link_motif)
AC_SUBST(include_xbae)
AC_SUBST(link_xbae)
AC_SUBST(include_xmhtml)
AC_SUBST(link_xmhtml)
AC_SUBST(include_sciplot)
AC_SUBST(link_sciplot)

AC_OUTPUT([Makefile oleobug doc/Makefile lib/Makefile src/Makefile intl/Makefile m4/Makefile])

dnl FIX ME
dnl I don't know what the po/ directory is supposed to do/contain, but the
dnl line below doesn't work for me.
dnl
dnl AC_OUTPUT([Makefile oleobug doc/Makefile lib/Makefile src/Makefile intl/Makefile m4/Makefile po/Makefile.in], [sed -e "/POTFILES =/r po/POTFILES" po/Makefile.in > po/Makefile])
