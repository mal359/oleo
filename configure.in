dnl
dnl This is the configuration file for GNU oleo.
dnl
dnl $Id: configure.in,v 1.41 1999/02/10 22:23:05 danny Exp $
dnl
dnl Process this file with autoconf to produce a configure script.
dnl
AC_INIT(src/init.c)

dnl
dnl Need to be processed with Autoconf >= 2.13
dnl
AC_PREREQ(2.13)

AM_INIT_AUTOMAKE([oleo], [1.6.14])
GNU_PACKAGE="GNU $PACKAGE"
AC_DEFINE_UNQUOTED(GNU_PACKAGE, "$GNU_PACKAGE")

dnl
dnl Use of this macro will keep unexpected things from happening if
dnl you're not familiar with automake.
dnl
dnl AM_MAINTAINER_MODE

dnl Define available languages
ALL_LINGUAS="en nl fr"

AC_ARG_PROGRAM
AM_CONFIG_HEADER(config.h)

dnl
dnl Find a C compiler before even detecting systems
dnl
AC_PROG_CC

dnl
dnl Detect strange environments
dnl	This needs to be just about the first thing done.
dnl
AC_AIX
AC_ISC_POSIX
AC_CYGWIN
AC_EXEEXT

dnl
dnl Now at last we can do checks for programs.
dnl
AC_PROG_YACC
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_RANLIB

AC_CHECK_PROG(HAVE_TEXI2HTML, texi2html, yes)
AM_CONDITIONAL(HaveTexi2Html, test x$HAVE_TEXI2HTML = xyes)

dnl
dnl Dirty trick
dnl
if test -d /usr/local ; then
	CFLAGS="$CFLAGS -I/usr/local/include"
	LIBS="$LIBS -L/usr/local/lib"
fi

dnl
dnl How good is the C compiler ?
dnl
AM_C_PROTOTYPES

dnl Checks for libraries.
AC_CHECK_LIB(ncurses, waddch, ,
	AC_CHECK_LIB(curses, addch, ,
		AC_CHECK_LIB(termlib, tgetent, ,
			AC_CHECK_LIB(termcap, tgetend))))

dnl This library is required on FreeBSD.
AC_CHECK_LIB(mytinfo, tputs)

dnl
dnl Check existence of a working math library
dnl	The good old check with sqrtf fails on some systems.
dnl	Is there a function that works on all ?
dnl	Yes: "main" just detects presence of the library.
dnl
AC_CHECK_LIB(m, main)

dnl Checks for X11
AC_PATH_XTRA

dnl Add link to X11
AC_LINK_X

dnl
dnl Motif (LessTif)
dnl
dnl If user selects Motif, deselect GTK even if the user requested it.
dnl
AC_ARG_WITH(motif,
[  --without-motif		  Do not use Motif, even if detected],
[		case "${withval}" in
		  y | ye | yes )	usemotif=yes ;;
		  n | no )		usemotif=no ;;
		  * )			usemotif=yes ;;
		esac],
[		with_motif=yes])

dnl
dnl If you ask not to have Xbae then you can't have Motif either.
dnl
test "$with_Xbae" = "no" && with_motif="no"
dnl
dnl Checks for Motif
dnl These should not happen if --without-motif has been used.
dnl
if test "$with_motif" = "yes"; then
  AC_FIND_MOTIF
  ICE_FIND_Xbae
  ICE_FIND_XmHTML
  AC_FIND_XLT
  if test "$have_xlt" = "yes"; then
    CFLAGS="$XLT_CFLAGS $MOTIF_CFLAGS $X_CFLAGS $CFLAGS"
    LIBS="$XLT_LIBS $MOTIF_LIBS $X_LIBS $X_PRE_LIBS -lXt -lX11 $X_EXTRA_LIBS $LIBS"
    AC_DEFINE(HAVE_SciPlot_H)
  else
    ICE_FIND_SciPlot
    CFLAGS="$MOTIF_CFLAGS $X_CFLAGS $CFLAGS"
    LIBS="$MOTIF_LIBS $X_LIBS $X_PRE_LIBS -lXt -lX11 $X_EXTRA_LIBS $LIBS"
  fi
fi

dnl
dnl This is an automake variable which allows you to use conditionals
dnl in makefiles. We'll use it in the src/ directory to figure out
dnl which files to compile.
dnl
AM_CONDITIONAL(UseMotif, test x$with_motif = xyes)

dnl
dnl Check whether a recent version of GNU PlotUtils is around
dnl	This needs to be done after the X and Motif tests, because
dnl	the libplot from GNU plotutils sometimes requires linking the
dnl	X and Motif libraries
dnl
AC_CHECK_LIB(plot, openpl)

dnl Checks for Guile
dnl AC_CHECK_LIB(guile, scm_boot_guile)

dnl Checks if -DDEBUG is requested
AC_ARG_WITH(debug,
[  --with-debug                   Send debugging info to stderr],
[              case "${withval}" in
                 y | ye | yes )         usedebug=yes ;;
                 n | no )               usedebug=no ;;
                 * )                    usedebug=yes ;;
               esac],
[              with_debug=no])

if test $with_debug = yes; then
  CFLAGS="$CFLAGS -DDEBUG"
  if test "x$GCC" = "xyes"; then
    if test -z "`echo "$CFLAGS" | grep "\-Wall" 2> /dev/null`"; then
      CFLAGS="$CFLAGS -Wall"
    fi
  fi
fi
           
dnl Checks for header files.
AC_HEADER_DIRENT
AC_HEADER_STDC
AC_CHECK_HEADERS(fcntl.h limits.h malloc.h strings.h sys/time.h unistd.h)
AC_CHECK_HEADERS(locale.h time.h)
AC_CHECK_HEADERS(xbase.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_INLINE
AC_TYPE_SIZE_T
AC_HEADER_TIME
AC_STRUCT_TM
AC_STRUCT_TIMEZONE

dnl Checks for library functions.
AC_FUNC_ALLOCA
AC_TYPE_SIGNAL
AC_FUNC_STRFTIME
AC_FUNC_VPRINTF
AC_CHECK_FUNCS(ftime gethostname gettimeofday mktime select strdup strstr)
AC_CHECK_FUNCS(putenv)
AC_CHECK_FUNCS(strerror)
AM_FUNC_MKTIME
AM_FUNC_OBSTACK

dnl See if we're GNU Gettext friendly
AM_GNU_GETTEXT

dnl
dnl This used to cause all kinds of problems in an earlier revision
dnl	of oleo.
dnl
AC_LINK_FILES($nls_cv_header_libgt, $nls_cv_header_intl)

dnl
dnl Xbase
dnl
dnl AC_ARG_WITH(xbase,
dnl [  --without-xbase		  Do not use Xbase, even if detected],
dnl [		case "${withval}" in
dnl 		  y | ye | yes )	usexbase=yes ;;
dnl 		  n | no )		usexbase=no ;;
dnl 		  * )			usexbase=yes ;;
dnl 		esac],
dnl [		usexbase=yes])
dnl 
dnl if test $usexbase = yes; then
dnl 	AC_DEFINE(USE_XBASE)
dnl fi

AC_SUBST(LIBOBJS)

AC_SUBST(include_motif)
AC_SUBST(link_motif)
AC_SUBST(include_xbae)
AC_SUBST(link_xbae)
AC_SUBST(include_xmhtml)
AC_SUBST(link_xmhtml)
AC_SUBST(include_sciplot)
AC_SUBST(link_sciplot)

AC_OUTPUT([Makefile oleobug doc/Makefile lib/Makefile src/Makefile intl/Makefile m4/Makefile po/Makefile.in Xresources/Makefile],[sed -e "/POTFILES =/r po/POTFILES" po/Makefile.in > po/Makefile])
